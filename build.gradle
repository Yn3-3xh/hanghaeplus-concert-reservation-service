plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'

    // openapi generator 적용
    id "org.openapi.generator" version "7.2.0"
}

group = 'hanghaeplus'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    configureEach {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    runtimeOnly 'com.mysql:mysql-connector-j'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // springdoc-openapi 적용
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.11'

    // openapi generator 적용
    implementation 'org.openapitools:openapi-generator:7.2.0'
    implementation 'org.openapitools:openapi-generator-gradle-plugin:7.2.0'

    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'jakarta.validation:jakarta.validation-api:3.0.0'
    implementation 'jakarta.annotation:jakarta.annotation-api:2.0.0'
    implementation 'jakarta.servlet:jakarta.servlet-api:6.0.0'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.2.1'
}

openApiGenerate {
    verbose.set(true)
    generatorName.set("spring")
    library.set("spring-boot")
    inputSpec.set(project.file("$rootDir/src/main/resources/openapi/openapi.yaml").absolutePath)
    outputDir.set(project.file("$buildDir/generated").absolutePath)
    apiPackage.set("org.openapi.api")
    modelPackage.set("org.openapi.model")
    configOptions.set([
            interfaceOnly: "true",
            useBeanValidation: "true",
            performBeanValidation: "true",
            serializableModel: "true",
            sourceFolder:"",
            implFolder: "/java",
            useSpringBoot3: "true",
            useJakarta: "true",
            useJakartaEe: "true",
            skipDefaultInterface: "true",
            dateLibrary: "java17",
            additionalModelTypeAnnotations: "@lombok.AllArgsConstructor @lombok.NoArgsConstructor",
    ])
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", "$buildDir/generated"]
        }
    }
}

tasks.named('compileJava') {
    dependsOn tasks.named('openApiGenerate')
}

tasks.named('processResources') {
    dependsOn tasks.named('openApiGenerate')
}

tasks.named('test') {
    useJUnitPlatform()
}
